package backend;

import flixel.FlxCamera;

import data.Song;
import objects.Note;

#if ACHIEVEMENTS_ALLOWED
import haxe.Exception;
import objects.AchievementPopup;
#if LUA_ALLOWED
import psychlua.FunkinLua.State;
#end

typedef Achievement =
{
	var name:String;
	var description:String;
	@:optional var hidden:Bool;
	@:optional var maxScore:Float;
	@:optional var maxDecimals:Int;

	//handled automatically, ignore these two
	@:optional var mod:String;
	@:optional var ID:Int;
}

class Achievements {
	/**
	 * Executes the `init` operation.
	 * @return Result produced by `init`, when applicable.
	 */
	public static function init()
	{
		createAchievement('friday_night_play',		{name: "Freaky on a Friday Night", description: "Play on a Friday... Night.", hidden: true});
		createAchievement('week1_nomiss',			{name: "She Calls Me Daddy Too", description: "Beat Week 1 on Hard with no Misses."});
		createAchievement('week2_nomiss',			{name: "No More Tricks", description: "Beat Week 2 on Hard with no Misses."});
		createAchievement('week3_nomiss',			{name: "Call Me The Hitman", description: "Beat Week 3 on Hard with no Misses."});
		createAchievement('week4_nomiss',			{name: "Lady Killer", description: "Beat Week 4 on Hard with no Misses."});
		createAchievement('week5_nomiss',			{name: "Missless Christmas", description: "Beat Week 5 on Hard with no Misses."});
		createAchievement('week6_nomiss',			{name: "Highscore!!", description: "Beat Week 6 on Hard with no Misses."});
		createAchievement('week7_nomiss',			{name: "God Effing Damn It!", description: "Beat Week 7 on Hard with no Misses."});
		createAchievement('weekend1_nomiss',		{name: "Just a Friendly Sparring", description: "Beat Weekend 1 on Hard with no Misses."});
		createAchievement('ur_bad',					{name: "What a Funkin' Disaster!", description: "Complete a Song with a rating lower than 20%."});
		createAchievement('ur_good',				{name: "Perfectionist", description: "Complete a Song with a rating of 100%."});
		createAchievement('roadkill_enthusiast',	{name: "Roadkill Enthusiast", description: "Watch the Henchmen die 50 times.", maxScore: 50, maxDecimals: 0});
		createAchievement('oversinging', 			{name: "Oversinging Much...?", description: "Hold down a note for 10 seconds."});
		createAchievement('hype',					{name: "Hyperactive", description: "Finish a Song without going Idle."});
		createAchievement('two_keys',				{name: "Just the Two of Us", description: "Finish a Song pressing only two keys."});
		createAchievement('toastier',				{name: "Toaster Gamer", description: "Have you tried to run the game on a toaster?"});
		createAchievement('debugger',				{name: "Debugger", description: "Beat the \"Test\" Stage from the Chart Editor.", hidden: true});

		//dont delete this thing below
		_originalLength = _sortID + 1;
	}
	public static var achievements:Map<String, Achievement> = new Map<String, Achievement>();
	public static var variables:Map<String, Float> = [];
	public static var achievementsUnlocked:Array<String> = [];
	private static var _firstLoad:Bool = true;

	/**
	 * Executes the `get` operation.
	 * @param name Input value for `name`.
	 * @return Result produced by `get`, when applicable.
	 */
	public static function get(name:String):Achievement
		return achievements.get(name);
	/**
	 * Executes the `exists` operation.
	 * @param name Input value for `name`.
	 * @return Result produced by `exists`, when applicable.
	 */
	public static function exists(name:String):Bool
		return achievements.exists(name);

	/**
	 * Executes the `load` operation.
	 */
	public static function load():Void
	{
		if(!_firstLoad) return;

		if(_originalLength < 0) init();

		if(FlxG.save.data != null) {
			if(FlxG.save.data.achievementsUnlocked != null)
				achievementsUnlocked = FlxG.save.data.achievementsUnlocked;

			var savedMap:Map<String, Float> = cast FlxG.save.data.achievementsVariables;
			if(savedMap != null)
			{
				for (key => value in savedMap)
				{
					variables.set(key, value);
				}
			}
			_firstLoad = false;
		}
	}

	/**
	 * Executes the `save` operation.
	 */
	public static function save():Void
	{
		FlxG.save.data.achievementsUnlocked = achievementsUnlocked;
		FlxG.save.data.achievementsVariables = variables;
	}

	/**
	 * Executes the `getScore` operation.
	 * @param name Input value for `name`.
	 * @return Result produced by `getScore`, when applicable.
	 */
	public static function getScore(name:String):Float
		return _scoreFunc(name, 0);

	/**
	 * Executes the `setScore` operation.
	 * @param name Input value for `name`.
	 * @param value Input value for `value`.
	 * @param saveIfNotUnlocked Input value for `saveIfNotUnlocked`.
	 * @return Result produced by `setScore`, when applicable.
	 */
	public static function setScore(name:String, value:Float, saveIfNotUnlocked:Bool = true):Float
		return _scoreFunc(name, 1, value, saveIfNotUnlocked);

	/**
	 * Executes the `addScore` operation.
	 * @param name Input value for `name`.
	 * @param value Input value for `value`.
	 * @param saveIfNotUnlocked Input value for `saveIfNotUnlocked`.
	 * @return Result produced by `addScore`, when applicable.
	 */
	public static function addScore(name:String, value:Float = 1, saveIfNotUnlocked:Bool = true):Float
		return _scoreFunc(name, 2, value, saveIfNotUnlocked);

	//mode 0 = get, 1 = set, 2 = add
	/**
	 * Executes the `_scoreFunc` operation.
	 * @param name Input value for `name`.
	 * @param mode Input value for `mode`.
	 * @param addOrSet Input value for `addOrSet`.
	 * @param saveIfNotUnlocked Input value for `saveIfNotUnlocked`.
	 * @return Result produced by `_scoreFunc`, when applicable.
	 */
	static function _scoreFunc(name:String, mode:Int = 0, addOrSet:Float = 1, saveIfNotUnlocked:Bool = true):Float
	{
		if(!variables.exists(name))
			variables.set(name, 0);

		if(achievements.exists(name))
		{
			var achievement:Achievement = achievements.get(name);
			if(achievement.maxScore < 1) throw new Exception('Achievement has score disabled or is incorrectly configured: $name');

			if(achievementsUnlocked.contains(name)) return achievement.maxScore;

			var val = addOrSet;
			switch(mode)
			{
				case 0: return variables.get(name); //get
				case 2: val += variables.get(name); //add
			}

			if(val >= achievement.maxScore)
			{
				unlock(name);
				val = achievement.maxScore;
			}
			variables.set(name, val);

			Achievements.save();
			if(saveIfNotUnlocked || val >= achievement.maxScore) FlxG.save.flush();
			return val;
		}
		return -1;
	}

	static var _lastUnlock:Int = -999;
	/**
	 * Executes the `unlock` operation.
	 * @param name Input value for `name`.
	 * @param autoStartPopup Input value for `autoStartPopup`.
	 * @return Result produced by `unlock`, when applicable.
	 */
	public static function unlock(name:String, autoStartPopup:Bool = true):String {
		if(!achievements.exists(name))
		{
			FlxG.log.error('Achievement "$name" does not exists!');
			throw new Exception('Achievement "$name" does not exists!');
			return null;
		}

		if(Achievements.isUnlocked(name)) return null;

		trace('Completed achievement "$name"');
		achievementsUnlocked.push(name);

		// earrape prevention
		var time:Int = openfl.Lib.getTimer();
		if(Math.abs(time - _lastUnlock) >= 100) //If last unlocked happened in less than 100 ms (0.1s) ago, then don't play sound
		{
			FlxG.sound.play(Paths.sound('confirmMenu'), 0.5);
			_lastUnlock = time;
		}

		Achievements.save();
		FlxG.save.flush();

		if(autoStartPopup) startPopup(name);
		return name;
	}
	/**
	 * Executes the `isUnlocked` operation.
	 * @param name Input value for `name`.
	 * @return Result produced by `isUnlocked`, when applicable.
	 */
	inline public static function isUnlocked(name:String)
		return achievementsUnlocked.contains(name);

	@:allow(objects.AchievementPopup)
	private static var _popups:Array<AchievementPopup> = [];

	public static var showingPopups(get, never):Bool;
	/**
	 * Executes the `get_showingPopups` operation.
	 * @return Result produced by `get_showingPopups`, when applicable.
	 */
	public static function get_showingPopups()
		return _popups.length > 0;

	/**
	 * Executes the `startPopup` operation.
	 * @param achieve Input value for `achieve`.
	 * @return Result produced by `startPopup`, when applicable.
	 */
	public static function startPopup(achieve:String, endFunc:Void->Void = null) {
		for (popup in _popups)
		{
			if(popup == null) continue;
			popup.intendedY += 150;
		}

		var newPop:AchievementPopup = new AchievementPopup(achieve, endFunc);
		_popups.push(newPop);
		//trace('Giving achievement ' + achieve);
	}

	// Map sorting cuz haxe is physically incapable of doing that by itself
	static var _sortID = 0;
	static var _originalLength = -1;
	/**
	 * Executes the `createAchievement` operation.
	 * @param name Input value for `name`.
	 * @param data Input value for `data`.
	 * @param mod Input value for `mod`.
	 * @return Result produced by `createAchievement`, when applicable.
	 */
	public static function createAchievement(name:String, data:Achievement, ?mod:String = null)
	{
		data.ID = _sortID;
		data.mod = mod;
		achievements.set(name, data);
		_sortID++;
	}

	#if MODS_ALLOWED
	/**
	 * Executes the `reloadList` operation.
	 * @return Result produced by `reloadList`, when applicable.
	 */
	public static function reloadList()
	{
		// remove modded achievements
		if((_sortID + 1) > _originalLength)
			for (key => value in achievements)
				if(value.mod != null)
					achievements.remove(key);

		_sortID = _originalLength-1;

		var modLoaded:String = Mods.currentModDirectory;
		Mods.currentModDirectory = null;
		loadAchievementJson(Paths.mods('data/achievements.json'));
		for (i => mod in Mods.getModDirectories())
		{
			Mods.currentModDirectory = mod;
			loadAchievementJson(Paths.mods('$mod/data/achievements.json'));
		}
		Mods.currentModDirectory = modLoaded;
	}

	/**
	 * Executes the `loadAchievementJson` operation.
	 * @param path Input value for `path`.
	 * @param addMods Input value for `addMods`.
	 * @return Result produced by `loadAchievementJson`, when applicable.
	 */
	inline static function loadAchievementJson(path:String, addMods:Bool = true)
	{
		var retVal:Array<Dynamic> = null;
		if(FileSystem.exists(path)) {
			try {
				var rawJson:String = File.getContent(path).trim();
				if(rawJson != null && rawJson.length > 0) retVal = Json.parse(rawJson); //Json.parse('{"achievements": $rawJson}').achievements;

				if(addMods && retVal != null)
				{
					for (i in 0...retVal.length)
					{
						var achieve:Dynamic = retVal[i];
						if(achieve == null)
						{
							var errorTitle = 'Mod name: ' + Mods.currentModDirectory != null ? Mods.currentModDirectory : "None";
							var errorMsg = 'Achievement #${i+1} is invalid.';
							#if windows
							lime.app.Application.current.window.alert(errorMsg, errorTitle);
							#end
							trace('$errorTitle - $errorMsg');
							continue;
						}

						var key:String = achieve.save;
						if(key == null || key.trim().length < 1)
						{
							var errorTitle = 'Error on Achievement: ' + (achieve.name != null ? achieve.name : achieve.save);
							var errorMsg = 'Missing valid "save" value.';
							#if windows
							lime.app.Application.current.window.alert(errorMsg, errorTitle);
							#end
							trace('$errorTitle - $errorMsg');
							continue;
						}
						key = key.trim();
						if(achievements.exists(key)) continue;

						createAchievement(key, achieve, Mods.currentModDirectory);
					}
				}
			} catch(e:Dynamic) {
				var errorTitle = 'Mod name: ' + Mods.currentModDirectory != null ? Mods.currentModDirectory : "None";
				var errorMsg = 'Error loading achievements.json: $e';
				#if windows
				lime.app.Application.current.window.alert(errorMsg, errorTitle);
				#end
				trace('$errorTitle - $errorMsg');
			}
		}
		return retVal;
	}

	#if LUA_ALLOWED
	/**
	 * Executes the `addLuaCallbacks` operation.
	 * @param lua Input value for `lua`.
	 * @return Result produced by `addLuaCallbacks`, when applicable.
	 */
	public static function addLuaCallbacks(lua:State)
	{
		Convert.addCallback(lua, "getAchievementScore", function(name:String):Float
		{
			if(!achievements.exists(name))
			{
				LuaUtils.luaTrace(lua, 'getAchievementScore: Couldnt find achievement: $name', false, false, FlxColor.RED);
				return -1;
			}
			return getScore(name);
		});
		Convert.addCallback(lua, "setAchievementScore", function(name:String, ?value:Float = 1, ?saveIfNotUnlocked:Bool = true):Float
		{
			if(!achievements.exists(name))
			{
				LuaUtils.luaTrace(lua, 'setAchievementScore: Couldnt find achievement: $name', false, false, FlxColor.RED);
				return -1;
			}
			return setScore(name, value, saveIfNotUnlocked);
		});
		Convert.addCallback(lua, "addAchievementScore", function(name:String, ?value:Float = 1, ?saveIfNotUnlocked:Bool = true):Float
		{
			if(!achievements.exists(name))
			{
				LuaUtils.luaTrace(lua, 'addAchievementScore: Couldnt find achievement: $name', false, false, FlxColor.RED);
				return -1;
			}
			return addScore(name, value, saveIfNotUnlocked);
		});
		Convert.addCallback(lua, "unlockAchievement", function(name:String):Dynamic
		{
			if(!achievements.exists(name))
			{
				LuaUtils.luaTrace(lua, 'unlockAchievement: Couldnt find achievement: $name', false, false, FlxColor.RED);
				return null;
			}
			return unlock(name);
		});
		Convert.addCallback(lua, "isAchievementUnlocked", function(name:String):Dynamic
		{
			if(!achievements.exists(name))
			{
				LuaUtils.luaTrace(lua, 'isAchievementUnlocked: Couldnt find achievement: $name', false, false, FlxColor.RED);
				return null;
			}
			return isUnlocked(name);
		});
		Convert.addCallback(lua, "achievementExists", function(name:String) return achievements.exists(name));
	}
	#end

	#if PYTHON_ALLOWED
	/**
	 * Executes the `addPythonCallbacks` operation.
	 * @param python Input value for `python`.
	 * @return Result produced by `addPythonCallbacks`, when applicable.
	 */
	public static function addPythonCallbacks(python:PythonScript)
	{
		python.set("getAchievementScore", function(name:String):Float
		{
			if(!achievements.exists(name))
			{
				python.pyTrace('getAchievementScore: Couldnt find achievement: $name', false, false, FlxColor.RED);
				return -1;
			}
			return getScore(name);
		});
		python.set("setAchievementScore", function(name:String, ?value:Float = 1, ?saveIfNotUnlocked:Bool = true):Float
		{
			if(!achievements.exists(name))
			{
				python.pyTrace('setAchievementScore: Couldnt find achievement: $name', false, false, FlxColor.RED);
				return -1;
			}
			return setScore(name, value, saveIfNotUnlocked);
		});
		python.set("addAchievementScore", function(name:String, ?value:Float = 1, ?saveIfNotUnlocked:Bool = true):Float
		{
			if(!achievements.exists(name))
			{
				python.pyTrace('addAchievementScore: Couldnt find achievement: $name', false, false, FlxColor.RED);
				return -1;
			}
			return addScore(name, value, saveIfNotUnlocked);
		});
		python.set("unlockAchievement", function(name:String):Dynamic
		{
			if(!achievements.exists(name))
			{
				python.pyTrace('unlockAchievement: Couldnt find achievement: $name', false, false, FlxColor.RED);
				return null;
			}
			return unlock(name);
		});
		python.set("isAchievementUnlocked", function(name:String):Dynamic
		{
			if(!achievements.exists(name))
			{
				python.pyTrace('isAchievementUnlocked: Couldnt find achievement: $name', false, false, FlxColor.RED);
				return null;
			}
			return isUnlocked(name);
		});
		python.set("achievementExists", function(name:String) return achievements.exists(name));
	}
	#end
	#end
}
#end
