package backend;

import objects.Note;
import headers.Objects;
import objects.NoteSplash;
import play.PlayState;
import flixel.animation.FlxAnimationController;
import flixel.graphics.FlxGraphic;
import flixel.graphics.frames.FlxFramesCollection;
import haxe.io.Bytes;
import haxe.io.Path;
#if lime_vorbis
import lime.media.AudioBuffer;
import lime.media.vorbis.VorbisFile;
#end
import openfl.display.BitmapData;
import openfl.display3D.textures.RectangleTexture;
import openfl.geom.Rectangle;
import openfl.media.Sound;
import openfl.system.System;
import openfl.utils.AssetType;

import data.Song;
#if cpp
import cpp.vm.Gc;
#elseif hl
import hl.Gc;
#end

@:access(openfl.display.BitmapData)
// @:nullSafety // not yet
class Paths
{
  inline public static var SOUND_EXT = #if (web || flash) "mp3" #else "ogg" #end;
  inline public static var VIDEO_EXT = "mp4";
  inline public static var IMAGE_EXT = "png";

  public static var defaultNoteSprite:FlxSprite;

  public static var noteSkinFramesMap:Map<String, FlxFramesCollection> = new Map();
  public static var noteSkinAnimsMap:Map<String, FlxAnimationController> = new Map();

  private static var splashFrames:FlxFramesCollection;
  private static var splashAnimation:FlxAnimationController;

  public static var splashSkinFramesMap:Map<String, FlxFramesCollection> = new Map();
  public static var splashSkinAnimsMap:Map<String, FlxAnimationController> = new Map();

  public static var splashConfigs:Map<String, objects.NoteSplash.NoteSplashConfig> = new Map();
  public static var splashAnimCountMap:Map<String, Int> = new Map();

  public static var defaultSkin = 'noteskins/NOTE_assets' + NoteHelpers.getNoteSkinPostfix();

  // Function that initializes the first note. This way, we can recycle the notes
  /**
   * Executes the `initDefaultSkin` operation.
   * @param noteSkin Input value for `noteSkin`.
   * @param inEditor Input value for `inEditor`.
   * @return Result produced by `initDefaultSkin`, when applicable.
   */
  public static function initDefaultSkin(?noteSkin:String, ?inEditor:Bool = false)
  {
    if (noteSkin.length > 0) defaultSkin = noteSkin;
    else if (!PlayState.isPixelStage) defaultSkin = 'noteskins/NOTE_assets' + NoteHelpers.getNoteSkinPostfix();
    else
      defaultSkin = 'noteskins/NOTE_assets';
    trace(defaultSkin);
  }

  /**
   * Executes the `initNote` operation.
   * @param noteSkin Input value for `noteSkin`.
   * @return Result produced by `initNote`, when applicable.
   */
  public static function initNote(?noteSkin:String)
  {
    // Do this to be able to just copy over the note animations and not reallocate it
    if (noteSkin.length < 1) noteSkin = defaultSkin;
    var spr:FlxSprite = new FlxSprite();
    spr.frames = getSparrowAtlas(noteSkin.length > 1 ? noteSkin : defaultSkin);

    // Use a for loop for adding all of the animations in the note spritesheet, otherwise it won't find the animations for the next recycle
    for (d in 0...Note.colArray.length)
    {
      if (d == 0) spr.animation.addByPrefix('purpleholdend', 'pruple end hold'); // ?????
      spr.animation.addByPrefix(Note.colArray[d] + 'holdend', Note.colArray[d] + ' hold end');
      spr.animation.addByPrefix(Note.colArray[d] + 'hold', Note.colArray[d] + ' hold piece');
      spr.animation.addByPrefix(Note.colArray[d] + 'Scroll', Note.colArray[d] + '0');
    }
    noteSkinFramesMap.set(noteSkin, spr.frames);
    noteSkinAnimsMap.set(noteSkin, spr.animation);
  }

  // Note Splash initialization
  /**
   * Executes the `initSplash` operation.
   * @param splashSkin Input value for `splashSkin`.
   * @return Result produced by `initSplash`, when applicable.
   */
  public static function initSplash(?splashSkin:String)
  {
    var skin:String = (splashSkin != null && splashSkin.length > 0) ? splashSkin : 'noteSplashes/noteSplashes' + NoteSplash.getSplashSkinPostfix();
    splashFrames = getSparrowAtlas(skin);
    if (splashFrames == null) splashFrames = getSparrowAtlas('noteSplashes/noteSplashes' + NoteSplash.getSplashSkinPostfix());

    // Do this to be able to just copy over the splash animations and not reallocate it

    var spr:FlxSprite = new FlxSprite();
    spr.frames = splashFrames;
    splashAnimation = new FlxAnimationController(spr);

    // Use a for loop for adding all of the animations in the splash spritesheet, otherwise it won't find the animations for the next recycle

    var config = splashConfigs.get(skin);
    if (config == null) config = initSplashConfig(skin);
    var maxAnims:Int = 0;
    var animName:String = (config != null && config.anim != null && config.anim.length > 0) ? config.anim : "note splash";

    var shouldBreakLoop = false;
    while (!shouldBreakLoop)
    {
      var animID:Int = maxAnims + 1;
      for (i in 0...Note.colArray.length)
      {
        if (!addAnimAndCheck('note$i-$animID', '$animName ${Note.colArray[i]} $animID', 24, false))
        {
          // Reached the maximum amount of anims, break the loop
          shouldBreakLoop = true;
          break;
        }
      }
      if (!shouldBreakLoop) maxAnims++;
      else
        break;
      // trace('currently: $maxAnims');
    }
    splashSkinFramesMap.set(splashSkin, splashFrames);
    splashSkinAnimsMap.set(splashSkin, splashAnimation);
    splashAnimCountMap.set(splashSkin, maxAnims);
  }

  /**
   * Executes the `addAnimAndCheck` operation.
   * @param name Input value for `name`.
   * @param anim Input value for `anim`.
   * @param framerate Input value for `framerate`.
   * @param loop Input value for `loop`.
   * @return Result produced by `addAnimAndCheck`, when applicable.
   */
  public static function addAnimAndCheck(name:String, anim:String, ?framerate:Int = 24, ?loop:Bool = false)
  {
    var animFrames = [];
    @:privateAccess
    splashAnimation.findByPrefix(animFrames, anim); // adds valid frames to animFrames

    if (animFrames.length < 1) return false;

    splashAnimation.addByPrefix(name, anim, framerate, loop);
    return true;
  }

  /**
   * Executes the `initSplashConfig` operation.
   * @param skin Input value for `skin`.
   * @return Result produced by `initSplashConfig`, when applicable.
   */
  public static function initSplashConfig(skin:String)
  {
    var path:String = Paths.getSharedPath('images/' + skin + '.txt');
    #if sys
    if (!FileSystem.exists(path)) path = Paths.modsTxt(skin);
    if (!FileSystem.exists(path)) path = Paths.getSharedPath('images/noteSplashes/noteSplashes' + NoteSplash.getSplashSkinPostfix() + '.txt');
    #end

    var configFile:Array<String> = CoolUtil.coolTextFile(path);

    if (configFile.length < 1) return null;

    var framerates:Array<String> = configFile[1].split(' ');
    var offs:Array<Array<Float>> = [];
    for (i in 2...configFile.length)
    {
      var animOffs:Array<String> = configFile[i].split(' ');
      offs.push([Std.parseFloat(animOffs[0]), Std.parseFloat(animOffs[1])]);
    }
    var config:objects.NoteSplash.NoteSplashConfig =
      {
        anim: configFile[0],
        minFps: Std.parseInt(framerates[0]),
        maxFps: Std.parseInt(framerates[1]),
        offsets: offs
      };
    splashConfigs.set(skin, config);
    return config;
  }

  /**
   * Executes the `excludeAsset` operation.
   * @param key Input value for `key`.
   * @return Result produced by `excludeAsset`, when applicable.
   */
  public static function excludeAsset(key:String)
  {
    if (!dumpExclusions.contains(key)) dumpExclusions.push(key);
  }

  public static var dumpExclusions:Array<String> = [
    'assets/music/freakyMenu.$SOUND_EXT',
    'assets/shared/music/breakfast.$SOUND_EXT',
    'assets/shared/music/tea-time.$SOUND_EXT',
  ];

  @:noCompletion private inline static function _gc(major:Bool)
  {
    #if cpp
    Gc.run(major);
    #elseif hl
    Gc.major();
    #end
  }

  @:noCompletion public inline static function compress()
  {
    #if cpp
    Gc.compact();
    #elseif hl
    Gc.major();
    #end
  }

  /**
   * Executes the `gc` operation.
   * @param major Input value for `major`.
   * @param repeat Input value for `repeat`.
   * @return Result produced by `gc`, when applicable.
   */
  public inline static function gc(major:Bool = false, repeat:Int = 1)
  {
    while (repeat-- > 0)
      _gc(major);
  }

  /// haya I love you for the base cache dump I took to the max
  /**
   * Executes the `clearUnusedMemory` operation.
   * @return Result produced by `clearUnusedMemory`, when applicable.
   */
  public static function clearUnusedMemory()
  {
    // clear non local assets in the tracked assets list
    for (key in currentTrackedAssets.keys())
    {
      // if it is not currently contained within the used local assets
      if (!localTrackedAssets.contains(key) && !dumpExclusions.contains(key))
      {
        destroyGraphic(currentTrackedAssets.get(key)); // get rid of the graphic
        currentTrackedAssets.remove(key); // and remove the key from local cache map
      }
    }
    // run the garbage collector for good measure lmfao
    compress();
    gc(true);
  }

  // define the locally tracked assets
  public static var localTrackedAssets:Array<String> = [];

  @:access(flixel.system.frontEnds.BitmapFrontEnd._cache)
  /**
   * Executes the `clearStoredMemory` operation.
   * @param cleanUnused Input value for `cleanUnused`.
   * @return Result produced by `clearStoredMemory`, when applicable.
   */
  public static function clearStoredMemory(?cleanUnused:Bool = false)
  {
    // clear anything not in the tracked assets list
    for (key in FlxG.bitmap._cache.keys())
    {
      if (!currentTrackedAssets.exists(key)) destroyGraphic(FlxG.bitmap.get(key));
    }
    // clear all sounds that are cached
    for (key in currentTrackedSounds.keys())
    {
      if (!localTrackedAssets.contains(key) && !dumpExclusions.contains(key) && key != null)
      {
        // trace('test: ' + dumpExclusions, key);
        Assets.cache.clear(key);
        currentTrackedSounds.remove(key);
      }
    }
    // flags everything to be cleared out next unused memory clear
    localTrackedAssets = [];
    #if !html5 openfl.Assets.cache.clear("songs"); #end
    gc(true);
    compress();
  }

  /**
   * Executes the `destroyGraphic` operation.
   * @param graphic Input value for `graphic`.
   * @return Result produced by `destroyGraphic`, when applicable.
   */
  inline static function destroyGraphic(graphic:FlxGraphic)
  {
    // free some gpu memory
    #if !flash
    if (graphic != null && graphic.bitmap != null && graphic.bitmap.__texture != null) graphic.bitmap.__texture.dispose();
    #end
    FlxG.bitmap.remove(graphic);
  }

  static public var currentLevel:String;

  /**
   * Executes the `setCurrentLevel` operation.
   * @param name Input value for `name`.
   * @return Result produced by `setCurrentLevel`, when applicable.
   */
  static public function setCurrentLevel(name:String)
  {
    currentLevel = name.toLowerCase();
  }

  /**
   * Executes the `getPath` operation.
   * @param file Input value for `file`.
   * @param type Input value for `type`.
   * @param library Input value for `library`.
   * @param modsAllowed Input value for `modsAllowed`.
   * @return Result produced by `getPath`, when applicable.
   */
  public static function getPath(file:String, ?type:AssetType = TEXT, ?library:Null<String> = null, ?modsAllowed:Bool = false):String
  {
    #if MODS_ALLOWED
    if (modsAllowed)
    {
      var customFile:String = file;
      if (library != null) customFile = '$library/$file';

      var modded:String = modFolders(customFile);
      if (FileSystem.exists(modded)) return modded;
    }
    #end

    if (library != null)
    {
      // trace(getLibraryPath(file, library));
      return getLibraryPath(file, library);
    }

    if (currentLevel != null)
    {
      var levelPath:String = '';
      if (currentLevel != 'shared')
      {
        levelPath = getLibraryPathForce(file, currentLevel);
        if (OpenFlAssets.exists(levelPath, type))
        {
          // trace(levelPath);
          return levelPath;
        }
      }

      levelPath = getLibraryPathForce(file, "shared");
      if (OpenFlAssets.exists(levelPath, type))
      {
        // trace(levelPath);
        return levelPath;
      }
    }
    // trace(getPreloadPath(file));
    return getPreloadPath(file);
  }

  /**
   * Executes the `readDirectory` operation.
   * @param path Input value for `path`.
   * @return Result produced by `readDirectory`, when applicable.
   */
  public static inline function readDirectory(path:String):Array<String>
  {
    #if sys
    return FileSystem.readDirectory(path);
    #else
    // original by Karim Akra
    var files:Array<String> = [];

    for (possibleFile in Assets.list().filter((f) -> f.contains(path)))
    {
      var file:String = possibleFile.replace('${path}/', "");
      if (file.contains("/")) file = file.replace(file.substring(file.indexOf("/"), file.length), "");

      if (!files.contains(file)) files.push(file);
    }

    files.sort((a, b) -> {
      a = a.toUpperCase();
      b = b.toUpperCase();
      return (a < b) ? -1 : (a > b) ? 1 : 0;
    });

    return files;
    #end
  }

  /**
   * Executes the `getLibraryPath` operation.
   * @param file Input value for `file`.
   * @param library Input value for `library`.
   * @return Result produced by `getLibraryPath`, when applicable.
   */
  static public function getLibraryPath(file:String, library = "preload")
  {
    return if (library == "preload" || library == "default") getPreloadPath(file); else getLibraryPathForce(file, library);
  }

  /**
   * Executes the `getLibraryPathForce` operation.
   * @param file Input value for `file`.
   * @param library Input value for `library`.
   * @param level Input value for `level`.
   * @return Result produced by `getLibraryPathForce`, when applicable.
   */
  inline static function getLibraryPathForce(file:String, library:String, ?level:String)
  {
    if (level == null) level = library;
    var returnPath = '$library:assets/$level/$file';
    if (OpenFlAssets.exists(returnPath))
    {
      return returnPath;
    } else
    {
      return 'assets/$level/$file';
    }
  }

  /**
   * Executes the `getPreloadPath` operation.
   * @param file Input value for `file`.
   * @return Result produced by `getPreloadPath`, when applicable.
   */
  inline public static function getPreloadPath(file:String = '')
  {
    return 'assets/$file';
  }

  /**
   * Executes the `getSharedPath` operation.
   * @param file Input value for `file`.
   * @return Result produced by `getSharedPath`, when applicable.
   */
  inline public static function getSharedPath(file:String = '')
  {
    return 'assets/shared/$file';
  }

  /**
   * Executes the `file` operation.
   * @param file Input value for `file`.
   * @param type Input value for `type`.
   * @param library Input value for `library`.
   * @return Result produced by `file`, when applicable.
   */
  inline static public function file(file:String, type:AssetType = TEXT, ?library:String)
  {
    return getPath(file, type, library);
  }

  /**
   * Executes the `txt` operation.
   * @param key Input value for `key`.
   * @param library Input value for `library`.
   * @return Result produced by `txt`, when applicable.
   */
  inline static public function txt(key:String, ?library:String)
  {
    return getPath('data/$key.txt', TEXT, library);
  }

  /**
   * Executes the `xml` operation.
   * @param key Input value for `key`.
   * @param library Input value for `library`.
   * @return Result produced by `xml`, when applicable.
   */
  inline static public function xml(key:String, ?library:String)
  {
    return getPath('data/$key.xml', TEXT, library);
  }

  /**
   * Executes the `json` operation.
   * @param key Input value for `key`.
   * @param library Input value for `library`.
   * @return Result produced by `json`, when applicable.
   */
  inline static public function json(key:String, ?library:String)
  {
    return getPath('data/' + key + '.json', TEXT, library);
  }

  /**
   * Executes the `shaderFragment` operation.
   * @param key Input value for `key`.
   * @param library Input value for `library`.
   * @return Result produced by `shaderFragment`, when applicable.
   */
  inline static public function shaderFragment(key:String, ?library:String)
  {
    return getPath('shaders/$key.frag', TEXT, library);
  }

  /**
   * Executes the `shaderVertex` operation.
   * @param key Input value for `key`.
   * @param library Input value for `library`.
   * @return Result produced by `shaderVertex`, when applicable.
   */
  inline static public function shaderVertex(key:String, ?library:String)
  {
    return getPath('shaders/$key.vert', TEXT, library);
  }

  /**
   * Executes the `lua` operation.
   * @param key Input value for `key`.
   * @param library Input value for `library`.
   * @return Result produced by `lua`, when applicable.
   */
  inline static public function lua(key:String, ?library:String)
  {
    return getPath('$key.lua', TEXT, library);
  }

  // Video loading (part of it)
  /**
   * Executes the `video` operation.
   * @param key Input value for `key`.
   * @param library Input value for `library`.
   * @return Result produced by `video`, when applicable.
   */
  static public function video(key:String, ?library:String = null)
  {
    #if MODS_ALLOWED
    var file:String = modsVideo(key);
    if (FileSystem.exists(file))
    {
      return file;
    }
    #end
    final path:String = ('assets/${(library != null) ? '$library/' : ''}videos/$key.$VIDEO_EXT');
    // trace('Video Path before being passed to hxCodec: $path');
    return path;
  }

  // Sound loading.
  /**
   * Executes the `sound` operation.
   * @param key Input value for `key`.
   * @param library Input value for `library`.
   * @return Result produced by `sound`, when applicable.
   */
  static public function sound(key:String, ?library:String):Sound
  {
    var sound:Sound = returnSound('sounds', key, library);
    return sound;
  }

  // Random sound loading.
  /**
   * Executes the `soundRandom` operation.
   * @param key Input value for `key`.
   * @param min Input value for `min`.
   * @param max Input value for `max`.
   * @param library Input value for `library`.
   * @return Result produced by `soundRandom`, when applicable.
   */
  inline static public function soundRandom(key:String, min:Int, max:Int, ?library:String)
  {
    return sound(key + FlxG.random.int(min, max), library);
  }

  // Music loading. Loads anything in assets/data/music, OR mods/data/music (if mods are allowed)
  /**
   * Executes the `music` operation.
   * @param key Input value for `key`.
   * @param library Input value for `library`.
   * @return Result produced by `music`, when applicable.
   */
  inline static public function music(key:String, ?library:String):Sound
  {
    var file:Sound = returnSound('music', key, library);
    return file;
  }

  // Loads the Voices. Crucial for generateSong
  /**
   * Executes the `voices` operation.
   * @param song Input value for `song`.
   * @param difficulty Input value for `difficulty`.
   * @param postfix Input value for `postfix`.
   * @return Result produced by `voices`, when applicable.
   */
  static public function voices(song:String, ?difficulty:String = '', ?postfix:String = null):Any
  {
    var formattedDifficulty:String = formatToSongPath(difficulty);
    if (difficulty.contains(' ')) difficulty = formattedDifficulty;
    #if (html5 || flash)
    return 'songs:assets/songs/${formatToSongPath(song)}/Voices.$SOUND_EXT';
    #else
    if (difficulty != null)
    {
      var songKey:String = '${formatToSongPath(song)}/Voices';
      if (postfix != null) songKey += '-' + postfix;
      songKey += '-$difficulty';
      if (FileSystem.exists(Paths.modFolders('songs/' + songKey + '.$SOUND_EXT'))
        || FileSystem.exists('assets/songs/' + songKey + '.$SOUND_EXT'))
      {
        var voices = returnSound('songs', songKey);
        return voices;
      }
    }
    var songKey:String = '${formatToSongPath(song)}/Voices';
    if (postfix != null) songKey += '-' + postfix;
    var voices = returnSound('songs', songKey);
    return voices;
    #end
  }

  // Loads the instrumental. Crucial for generateSong
  /**
   * Executes the `inst` operation.
   * @param song Input value for `song`.
   * @param difficulty Input value for `difficulty`.
   * @return Result produced by `inst`, when applicable.
   */
  static public function inst(song:String, ?difficulty:String = ''):Any
  {
    var formattedDifficulty:String = formatToSongPath(difficulty);
    if (difficulty.contains(' ')) difficulty = formattedDifficulty;
    #if (html5 || flash)
    return 'songs:assets/songs/${formatToSongPath(song)}/Inst.$SOUND_EXT';
    #else
    if (difficulty != null)
    {
      var songKey:String = '${formatToSongPath(song)}/Inst-$difficulty';
      if (FileSystem.exists(Paths.modFolders('songs/' + songKey + '.$SOUND_EXT'))
        || FileSystem.exists('assets/songs/' + songKey + '.$SOUND_EXT'))
      {
        var inst = returnSound('songs', songKey);
        return inst;
      }
    }
    var songKey:String = '${formatToSongPath(song)}/Inst';
    var inst = returnSound('songs', songKey);
    return inst;
    #end
  }

  /**
   * Executes the `playMenuMusic` operation.
   * @param force Input value for `force`.
   * @param volume Input value for `volume`.
   */
  static public function playMenuMusic(force:Bool = false, volume:Float = 1):Void
  {
    if (FlxG.sound.music == null || force)
    {
      final playAprilFools:Bool = DateUtils.isAprilFools();

      if (playAprilFools)
      {
        FlxG.sound.playMusic(Paths.music('aprilFools'), volume);
      } 
	  else
      {
        final musicName = 'freakyMenu-' + ClientPrefs.daMenuMusic;
        // trace(musicName);

        #if MODS_ALLOWED
        playModMusic(musicName, 'freakyMenu', volume); // TODO: add HScript support here
        #else
        FlxG.sound.playMusic(music(musicName), volume);
        #end
      }
    }
  }

  // For song events.
  /**
   * Executes the `songEvents` operation.
   * @param song Input value for `song`.
   * @param difficulty Input value for `difficulty`.
   * @param onlyEventsString Input value for `onlyEventsString`.
   * @return Result produced by `songEvents`, when applicable.
   */
  static public function songEvents(song:String, ?difficulty:String, ?onlyEventsString:Bool = false):String
  {
    if (difficulty != null)
    {
      var formattedDifficulty:String = formatToSongPath(difficulty);
      if (difficulty.contains(' ')) difficulty = formattedDifficulty;

      var eventsKey:String = formatToSongPath(song) + '/events-${difficulty.toLowerCase()}';
      #if sys
      if (FileSystem.exists(Paths.json(eventsKey))
        || FileSystem.exists(Paths.modsJson(eventsKey))) return (!onlyEventsString ? eventsKey : 'events-${difficulty.toLowerCase()}');
      #else
      if (OpenFlAssets.exists(Paths.json(eventsKey))) return (!onlyEventsString ? eventsKey : 'events-${difficulty.toLowerCase()}');
      #end
    }
    var eventsKey:String = formatToSongPath(song) + '/events';
    return (!onlyEventsString ? eventsKey : 'events');
  }

  /**
   * Executes the `imagePath` operation.
   * @param key Input value for `key`.
   * @param folder Input value for `folder`.
   * @return Result produced by `imagePath`, when applicable.
   */
  inline public static function imagePath(key:String, ?folder:String):String
    return getPath('images/$key.$IMAGE_EXT', IMAGE, folder);

  /**
   * Executes the `imageExists` operation.
   * @param key Input value for `key`.
   * @param folder Input value for `folder`.
   * @return Result produced by `imageExists`, when applicable.
   */
  inline public static function imageExists(key:String, ?folder:String):Bool
    return Paths.exists(imagePath(key, folder));

  // Loads images.
  public static var currentTrackedAssets:Map<String, FlxGraphic> = [];
  // because the trace is so damn annoying sometimes
  public static var warnedMissingAssets:Map<String, Bool> = [];

  /**
   * Executes the `image` operation.
   * @param key Input value for `key`.
   * @param parentFolder Input value for `parentFolder`.
   * @return Result produced by `image`, when applicable.
   */
  static public function image(key:String, ?parentFolder:String = null):FlxGraphic
  {
    key = 'images/$key' + '.png';
    var bitmap:BitmapData = null;
    if (currentTrackedAssets.exists(key))
    {
      localTrackedAssets.push(key);
      return currentTrackedAssets.get(key);
    }
    return cacheBitmap(key, parentFolder, bitmap);
  }

  /**
   * Executes the `cacheBitmap` operation.
   * @param key Input value for `key`.
   * @param parentFolder Input value for `parentFolder`.
   * @param bitmap Input value for `bitmap`.
   * @return Result produced by `cacheBitmap`, when applicable.
   */
  public static function cacheBitmap(key:String, ?parentFolder:String = null, ?bitmap:BitmapData):FlxGraphic
  {
    if (bitmap == null)
    {
      var file:String = getPath(key, IMAGE, parentFolder, true);
      #if MODS_ALLOWED if (FileSystem.exists(file)) bitmap = BitmapData.fromFile(file);
      else #end if (OpenFlAssets.exists(file, IMAGE)) bitmap = OpenFlAssets.getBitmapData(file);

      if (bitmap == null)
      {
 		if (!warnedMissingAssets.exists(file))
		{
			warnedMissingAssets.set(file, true);
			trace('oh no its returning null NOOOO ($file)');
		}
		return null;
      }
    }

    #if !flash
    if (ClientPrefs.cacheOnGPU && bitmap.image != null)
    {
      bitmap.lock();
      if (bitmap.__texture == null)
      {
        bitmap.image.premultiplied = true;
        bitmap.getTexture(FlxG.stage.context3D);
      }
      bitmap.getSurface();
      bitmap.disposeImage();
      bitmap.image.data = null;
      bitmap.image = null;
      bitmap.readable = true;
    }
    #end

    var graph:FlxGraphic = FlxGraphic.fromBitmapData(bitmap, false, key);
    graph.persist = true;
    graph.destroyOnNoUse = false;

    currentTrackedAssets.set(key, graph);
    localTrackedAssets.push(key);
    return graph;
  }

  /**
   * Executes the `getTextFromFile` operation.
   * @param key Input value for `key`.
   * @param ignoreMods Input value for `ignoreMods`.
   * @return Result produced by `getTextFromFile`, when applicable.
   */
  static public function getTextFromFile(key:String, ?ignoreMods:Bool = false):String
  {
    var text:String = null;
    #if sys
    #if MODS_ALLOWED
    if (text == null && !ignoreMods && FileSystem.exists(modFolders(key))) text = File.getContent(modFolders(key));
    #end

    if (text == null && FileSystem.exists(getPreloadPath(key))) text = File.getContent(getPreloadPath(key));

    if (text == null && currentLevel != null)
    {
      var levelPath:String = '';
      if (currentLevel != 'shared')
      {
        levelPath = getLibraryPathForce(key, currentLevel);
        if (FileSystem.exists(levelPath)) text = File.getContent(levelPath);
      }

      if (text == null)
      {
        levelPath = getLibraryPathForce(key, 'shared');
        if (FileSystem.exists(levelPath)) text = File.getContent(levelPath);
      }
    }
    #end
    if (text == null) text = Assets.getText(getPath(key, TEXT));

    if (text.charCodeAt(0) == 0xFEFF) text = text.substr(1); //Strip BOM to prevent Json.parse errors
    return text;
  }

  /**
   * Executes the `font` operation.
   * @param key Input value for `key`.
   * @return Result produced by `font`, when applicable.
   */
  inline static public function font(key:String)
  {
    #if MODS_ALLOWED
    var file:String = modsFont(key);
    if (FileSystem.exists(file))
    {
      return file;
    }
    #end
    return 'assets/fonts/$key';
  }

  /**
   * Executes the `fileExists` operation.
   * @param key Input value for `key`.
   * @param type Input value for `type`.
   * @param ignoreMods Input value for `ignoreMods`.
   * @return Result produced by `fileExists`, when applicable.
   */
  public static function fileExists(key:String, type:AssetType, ?ignoreMods:Bool = false)
  {
    #if MODS_ALLOWED
    if (!ignoreMods)
    {
      for (mod in Mods.getGlobalMods())
        if (FileSystem.exists(mods('$mod/$key'))) return true;

      if (FileSystem.exists(mods(Mods.currentModDirectory + '/' + key)) || FileSystem.exists(mods(key))) return true;

      if (FileSystem.exists(mods('$key'))) return true;
    }
    #end

    if (OpenFlAssets.exists(getPath(key, type)))
    {
      return true;
    }
    return false;
  }

  // temp shit lol
  /**
   * Executes the `exists` operation.
   * @param key Input value for `key`.
   * @param type Input value for `type`.
   * @param library Input value for `library`.
   * @return Result produced by `exists`, when applicable.
   */
  inline static public function exists(key:String, type:AssetType = null, ?library:String)
  {
    #if sys
    if (FileSystem.exists(key))
    {
      return true;
    }
    #end

    if (OpenFlAssets.exists(key, type))
    {
      return true;
    }
    return false;
  }

  /**
   * Executes the `existsPath` operation.
   * @param key Input value for `key`.
   * @param type Input value for `type`.
   * @param library Input value for `library`.
   * @return Result produced by `existsPath`, when applicable.
   */
  inline static public function existsPath(key:String, type:AssetType = null, ?library:String)
  {
    #if sys
    if (FileSystem.exists(getPath(key, type, library)))
    {
      return true;
    }
    #end

    if (OpenFlAssets.exists(getPath(key, type, library), type))
    {
      return true;
    }
    return false;
  }

  /**
   * Executes the `getContent` operation.
   * @param path Input value for `path`.
   * @return Result produced by `getContent`, when applicable.
   */
  inline public static function getContent(path:String)
  {
    #if sys
    if (path.contains(':')) path = path.substring(path.indexOf(':') + 1);
    if (FileSystem.exists(path)) return File.getContent(path);
    return null;
    #else
    return OpenFlAssets.getText(path);
    #end
  }

  /**
   * Executes the `getAtlas` operation.
   * @param key Input value for `key`.
   * @param parentFolder Input value for `parentFolder`.
   * @return Result produced by `getAtlas`, when applicable.
   */
  static public function getAtlas(key:String, ?parentFolder:String = null):FlxAtlasFrames
  {
    var useMod = false;
    var imageLoaded:FlxGraphic = image(key, parentFolder);

    var myXml:Dynamic = getPath('images/$key.xml', TEXT, parentFolder, true);
    if (OpenFlAssets.exists(myXml) #if MODS_ALLOWED || (FileSystem.exists(myXml) && (useMod = true)) #end)
    {
      #if MODS_ALLOWED
      return FlxAtlasFrames.fromSparrow(imageLoaded, (useMod ? File.getContent(myXml) : myXml));
      #else
      return FlxAtlasFrames.fromSparrow(imageLoaded, myXml);
      #end
    } else
    {
      var myJson:Dynamic = getPath('images/$key.json', TEXT, parentFolder, true);
      if (OpenFlAssets.exists(myJson) #if MODS_ALLOWED || (FileSystem.exists(myJson) && (useMod = true)) #end)
      {
        #if MODS_ALLOWED
        return FlxAtlasFrames.fromTexturePackerJson(imageLoaded, (useMod ? File.getContent(myJson) : myJson));
        #else
        return FlxAtlasFrames.fromTexturePackerJson(imageLoaded, myJson);
        #end
      }
    }
    return getPackerAtlas(key, parentFolder);
  }

  /**
   * Executes the `getMultiAtlas` operation.
   * @param keys Input value for `keys`.
   * @param parentFolder Input value for `parentFolder`.
   * @return Result produced by `getMultiAtlas`, when applicable.
   */
  static public function getMultiAtlas(keys:Array<String>, ?parentFolder:String = null):FlxAtlasFrames
  {
    var parentFrames:FlxAtlasFrames = Paths.getAtlas(keys[0].trim());
    if (keys.length > 1)
    {
      var original:FlxAtlasFrames = parentFrames;
      parentFrames = new FlxAtlasFrames(parentFrames.parent);
      parentFrames.addAtlas(original, true);
      for (i in 1...keys.length)
      {
        var extraFrames:FlxAtlasFrames = Paths.getAtlas(keys[i].trim(), parentFolder);
        if (extraFrames != null) parentFrames.addAtlas(extraFrames, true);
      }
    }
    return parentFrames;
  }

  /**
   * Executes the `getSparrowAtlas` operation.
   * @param key Input value for `key`.
   * @param parentFolder Input value for `parentFolder`.
   * @return Result produced by `getSparrowAtlas`, when applicable.
   */
  inline static public function getSparrowAtlas(key:String, ?parentFolder:String = null):FlxAtlasFrames
  {
    var imageLoaded:FlxGraphic = image(key, parentFolder);
    #if MODS_ALLOWED
    var xmlExists:Bool = false;

    var xml:String = modsXml(key);
    if (FileSystem.exists(xml)) xmlExists = true;

    return FlxAtlasFrames.fromSparrow(imageLoaded, (xmlExists ? File.getContent(xml) : getPath('images/$key' + '.xml', TEXT, parentFolder)));
    #else
    return FlxAtlasFrames.fromSparrow(imageLoaded, getPath('images/$key' + '.xml', TEXT, parentFolder));
    #end
  }

  /**
   * Executes the `getPackerAtlas` operation.
   * @param key Input value for `key`.
   * @param parentFolder Input value for `parentFolder`.
   * @return Result produced by `getPackerAtlas`, when applicable.
   */
  inline static public function getPackerAtlas(key:String, ?parentFolder:String = null):FlxAtlasFrames
  {
    var imageLoaded:FlxGraphic = image(key, parentFolder);
    #if MODS_ALLOWED
    var txtExists:Bool = false;

    var txt:String = modsTxt(key);
    if (FileSystem.exists(txt)) txtExists = true;

    return FlxAtlasFrames.fromSpriteSheetPacker(imageLoaded, (txtExists ? File.getContent(txt) : getPath('images/$key' + '.txt', TEXT, parentFolder)));
    #else
    return FlxAtlasFrames.fromSpriteSheetPacker(imageLoaded, getPath('images/$key' + '.txt', TEXT, parentFolder));
    #end
  }

  /**
   * Executes the `getAsepriteAtlas` operation.
   * @param key Input value for `key`.
   * @param parentFolder Input value for `parentFolder`.
   * @param allowGPU Input value for `allowGPU`.
   * @return Result produced by `getAsepriteAtlas`, when applicable.
   */
  inline static public function getAsepriteAtlas(key:String, ?parentFolder:String = null, ?allowGPU:Bool = true):FlxAtlasFrames
  {
    var imageLoaded:FlxGraphic = image(key, parentFolder);
    #if MODS_ALLOWED
    var jsonExists:Bool = false;

    var json:String = modsImagesJson(key);
    if (FileSystem.exists(json)) jsonExists = true;

    return FlxAtlasFrames.fromTexturePackerJson(imageLoaded, (jsonExists ? File.getContent(json) : getPath('images/$key' + '.json', TEXT, parentFolder)));
    #else
    return FlxAtlasFrames.fromTexturePackerJson(imageLoaded, getPath('images/$key' + '.json', TEXT, parentFolder));
    #end
  }

  /**
   * Executes the `formatToSongPath` operation.
   * @param path Input value for `path`.
   * @return Result produced by `formatToSongPath`, when applicable.
   */
  inline static public function formatToSongPath(path:String)
  {
    var invalidChars = ~/[~&\\;:<>#]/;
    var hideChars = ~/[.,'"%?!]/;

    var path = invalidChars.split(path.replace(' ', '-')).join("-");
    return hideChars.split(path).join("").toLowerCase();
  }

  // completely rewritten asset loading? fuck!
  public static var currentTrackedSounds:Map<String, Sound> = [];

  // Returns sounds which is useful for all the sfx
  /**
   * Executes the `returnSound` operation.
   * @param path Input value for `path`.
   * @param key Input value for `key`.
   * @param library Input value for `library`.
   * @param stream Input value for `stream`.
   * @return Result produced by `returnSound`, when applicable.
   */
  public static function returnSound(path:String, key:String, ?library:String, stream:Bool = false)
  {
    var sound:Sound = null;
    var file:String = null;

    #if MODS_ALLOWED
    file = modsSounds(path, key);
    if (currentTrackedSounds.exists(file))
    {
      localTrackedAssets.push(file);
      return currentTrackedSounds.get(file);
    } else if (FileSystem.exists(file))
    {
      #if lime_vorbis
      if (stream) sound = Sound.fromAudioBuffer(AudioBuffer.fromVorbisFile(VorbisFile.fromFile(file)));
      else
      #end
      try
      {
        final header:Bytes = File.getBytes(file).sub(0, 4);
        if (header.toString() != "OggS" && file != null)
        {
          throw 'The file "$file" is not a valid OGG file (missing OggS header). It may have been renamed from another format like MP3.';
        }

        sound = Sound.fromFile(file);
      }
      catch (e)
      {
        throw 'Cannot load sound file: $file\nMake sure it is a properly encoded .ogg file.\nError: $e';
      }
    } else
    #end
    {
      // I hate this so god damn much
      var gottenPath:String = getPath('$path/$key.$SOUND_EXT', SOUND, library);
      file = gottenPath.substring(gottenPath.indexOf(':') + 1, gottenPath.length);
      if (path == 'songs') gottenPath = 'songs:' + gottenPath;
      if (currentTrackedSounds.exists(file))
      {
        localTrackedAssets.push(file);
        return currentTrackedSounds.get(file);
      } else if (OpenFlAssets.exists(gottenPath, SOUND))
      {
        #if lime_vorbis
        if (stream) sound = OpenFlAssets.getMusic(gottenPath);
        else
        #end
        sound = OpenFlAssets.getSound(gottenPath);
      }
    }

    if (sound != null)
    {
      localTrackedAssets.push(file);
      currentTrackedSounds.set(file, sound);
      return sound;
    }

    trace('oh no its returning null NOOOO ($file)');
    return null;
  }

  #if MODS_ALLOWED
  // Loads mods.
  /**
   * Executes the `mods` operation.
   * @param key Input value for `key`.
   * @return Result produced by `mods`, when applicable.
   */
  inline static public function mods(key:String = '')
  {
    return 'mods/' + key;
  }

  // Loads fonts in mods/fonts.
  /**
   * Executes the `modsFont` operation.
   * @param key Input value for `key`.
   * @return Result produced by `modsFont`, when applicable.
   */
  inline static public function modsFont(key:String)
  {
    return modFolders('fonts/' + key);
  }

  // Loads jsons in mods/data.
  /**
   * Executes the `modsJson` operation.
   * @param key Input value for `key`.
   * @return Result produced by `modsJson`, when applicable.
   */
  inline static public function modsJson(key:String)
  {
    return modFolders('data/' + key + '.json');
  }

  // Loads videos in mods/videos.
  /**
   * Executes the `modsVideo` operation.
   * @param key Input value for `key`.
   * @return Result produced by `modsVideo`, when applicable.
   */
  inline static public function modsVideo(key:String)
  {
    return modFolders('videos/' + key + '.' + VIDEO_EXT);
  }

  // Loads sounds in mods/sounds.
  /**
   * Executes the `modsSounds` operation.
   * @param path Input value for `path`.
   * @param key Input value for `key`.
   * @return Result produced by `modsSounds`, when applicable.
   */
  inline static public function modsSounds(path:String, key:String)
  {
    return modFolders(path + '/' + key + '.' + SOUND_EXT);
  }

  // Loads images in mods/images.
  /**
   * Executes the `modsImages` operation.
   * @param key Input value for `key`.
   * @return Result produced by `modsImages`, when applicable.
   */
  inline static public function modsImages(key:String)
  {
    return modFolders('images/' + key + '.png');
  }

  // Loads xml files in mods/images.
  /**
   * Executes the `modsXml` operation.
   * @param key Input value for `key`.
   * @return Result produced by `modsXml`, when applicable.
   */
  inline static public function modsXml(key:String)
  {
    return modFolders('images/' + key + '.xml');
  }

  // Loads txt files in mods/images.
  /**
   * Executes the `modsTxt` operation.
   * @param key Input value for `key`.
   * @return Result produced by `modsTxt`, when applicable.
   */
  inline static public function modsTxt(key:String)
  {
    return modFolders('images/' + key + '.txt');
  }

  /**
   * Executes the `modsImagesJson` operation.
   * @param key Input value for `key`.
   * @return Result produced by `modsImagesJson`, when applicable.
   */
  inline static public function modsImagesJson(key:String)
    return modFolders('images/' + key + '.json');

  /**
   * Executes the `playModMusic` operation.
   * @param file Input value for `file`.
   * @param fallback Input value for `fallback`.
   * @param volume Input value for `volume`.
   */
  public static function playModMusic(file:String, fallback:String, volume:Float = 1):Void
  {
    final moddedNew = Paths.modFolders('music/' + file + '.ogg');
    final moddedLegacy = Paths.modFolders('music/${fallback}.ogg');
    for (track in [moddedNew, moddedLegacy])
    {
      // trace('track: ' + track);
      if (FileSystem.exists(track))
      {
        FlxG.sound.playMusic(Sound.fromFile(track), volume, true);
        // trace('curTrack: ' + track);
        return;
      }
    }
    final basePath = Paths.getPath('music/' + file + '.ogg', SOUND);
    if (OpenFlAssets.exists(basePath))
    {
      FlxG.sound.playMusic(Paths.music(file), volume);
      trace('fallback to base selected theme: ' + file);
    } else
    {
      FlxG.sound.playMusic(Paths.music(fallback), volume);
      trace('fallback to default: ' + fallback);
    }
  }

  /**
   * Executes the `modFolders` operation.
   * @param key Input value for `key`.
   * @return Result produced by `modFolders`, when applicable.
   */
  static public function modFolders(key:String)
  {
    if (Mods.currentModDirectory != null && Mods.currentModDirectory.length > 0)
    {
      var fileToCheck:String = mods(Mods.currentModDirectory + '/' + key);
      if (FileSystem.exists(fileToCheck))
      {
        return fileToCheck;
      }
    }

    for (mod in Mods.getGlobalMods())
    {
      var fileToCheck:String = mods(mod + '/' + key);
      if (FileSystem.exists(fileToCheck)) return fileToCheck;
    }
    return 'mods/' + key;
  }

  /**
   * Executes the `getBackupFilePath` operation.
   * @param songPath Input value for `songPath`.
   * @param diff Input value for `diff`.
   * @return Result produced by `getBackupFilePath`, when applicable.
   */
  public static function getBackupFilePath(songPath:String, diff:String):String
  {
    final fileName = songPath + "-" + diff + ".json";
    return Paths.modsJson("$songPath/$fileName");
  }
  #end

  #if flxanimate
  /**
   * Executes the `loadAnimateAtlas` operation.
   * @param spr Input value for `spr`.
   * @param folderOrImg Input value for `folderOrImg`.
   * @param spriteJson Input value for `spriteJson`.
   * @param animationJson Input value for `animationJson`.
   * @return Result produced by `loadAnimateAtlas`, when applicable.
   */
  public static function loadAnimateAtlas(spr:FlxAnimate, folderOrImg:Dynamic, spriteJson:Dynamic = null, animationJson:Dynamic = null)
  {
    var changedAnimJson = false;
    var changedAtlasJson = false;
    var changedImage = false;

    if (spriteJson != null)
    {
      changedAtlasJson = true;
      #if sys
      spriteJson = File.getContent(spriteJson);
      #else
      spriteJson = Assets.getText(spriteJson);
      #end
    }

    if (animationJson != null)
    {
      changedAnimJson = true;
      #if sys
      animationJson = File.getContent(animationJson);
      #else
      animationJson = Assets.getText(animationJson);
      #end
    }

    // is folder or image path
    if (Std.isOfType(folderOrImg, String))
    {
      var originalPath:String = folderOrImg;
      for (i in 0...10)
      {
        var st:String = '$i';
        if (i == 0) st = '';

        if (!changedAtlasJson)
        {
          if (fileExists('images/$originalPath/spritemap$st.json', TEXT, false))
          {
            spriteJson = getTextFromFile('images/$originalPath/spritemap$st.json');
            if (spriteJson != null)
            {
              // trace('found Sprite Json');
              changedImage = true;
              changedAtlasJson = true;
              folderOrImg = image('$originalPath/spritemap$st');
              break;
            }
          }
        } else if (fileExists('images/$originalPath/spritemap$st.png', IMAGE))
        {
          // trace('found Sprite PNG');
          changedImage = true;
          folderOrImg = image('$originalPath/spritemap$st');
          break;
        }
      }

      if (!changedImage)
      {
        // trace('Changing folderOrImg to FlxGraphic');
        changedImage = true;
        folderOrImg = image(originalPath);
      }

      if (!changedAnimJson)
      {
        // trace('found Animation Json');
        changedAnimJson = true;
        animationJson = getTextFromFile('images/$originalPath/Animation.json');
      }
    }
    spr.loadAtlasEx(folderOrImg, spriteJson, animationJson);
  }
  #end
}
