package stages.objects;
import backend.ClientPrefs;
import backend.Paths;

#if funkin.vis
import funkin.vis.dsp.SpectralAnalyzer;
#end

class ABotSpeaker extends FlxSpriteGroup
{
	final VIZ_MAX = 7; //ranges from viz1 to viz7
	final VIZ_POS_X:Array<Float> = [0, 59, 56, 66, 54, 52, 51];
	final VIZ_POS_Y:Array<Float> = [0, -8, -3.5, -0.4, 0.5, 4.7, 7];

	public var bg:FlxSprite;
	public var vizSprites:Array<FlxSprite> = [];
	public var eyeBg:FlxSprite;
	#if flxanimate
	public var eyes:FlxAnimate;
	public var speaker:FlxAnimate;
	#else
	public var eyes:FlxSprite;
	public var speaker:FlxSprite;
	#end

	#if funkin.vis
	var analyzer:SpectralAnalyzer;
	#end
	var volumes:Array<Float> = [];

	public var snd(default, set):FlxSound;
	/**
	 * Executes the `set_snd` operation.
	 * @param changed Input value for `changed`.
	 * @return Result produced by `set_snd`, when applicable.
	 */
	function set_snd(changed:FlxSound)
	{
		snd = changed;
		#if funkin.vis
		initAnalyzer();
		#end
		return snd;
	}

	/**
	 * Executes the `new` operation.
	 * @param x Input value for `x`.
	 * @param y Input value for `y`.
	 */
	public function new(x:Float = 0, y:Float = 0)
	{
		super(x, y);

		var antialias = ClientPrefs.globalAntialiasing;

		bg = new FlxSprite(90, 20).loadGraphic(Paths.image('abot/stereoBG'));
		bg.antialiasing = antialias;
		add(bg);

		var vizX:Float = 0;
		var vizY:Float = 0;
		var vizFrames = Paths.getSparrowAtlas('abot/aBotViz');
		for (i in 1...VIZ_MAX+1)
		{
			volumes.push(0.0);
			vizX += VIZ_POS_X[i-1];
			vizY += VIZ_POS_Y[i-1];
			var viz:FlxSprite = new FlxSprite(vizX + 140, vizY + 74);
			viz.frames = vizFrames;
			viz.animation.addByPrefix('VIZ', 'viz$i', 0);
			viz.animation.play('VIZ', true);
			viz.animation.curAnim.finish(); //make it go to the lowest point
			viz.antialiasing = antialias;
			vizSprites.push(viz);
			viz.updateHitbox();
			viz.centerOffsets();
			add(viz);
		}

		eyeBg = new FlxSprite(-30, 215).makeGraphic(1, 1, FlxColor.WHITE);
		eyeBg.scale.set(160, 60);
		eyeBg.updateHitbox();
		add(eyeBg);

		#if flxanimate
		eyes = new FlxAnimate(-10, 230);
		Paths.loadAnimateAtlas(eyes, 'abot/systemEyes');
		eyes.anim.addBySymbolIndices('lookleft', 'a bot eyes lookin', [0, 1, 2, 3, 4, 5, 6, 7, 8, 9, 10, 11, 12, 13, 14, 15, 16, 17], 24, false);
		eyes.anim.addBySymbolIndices('lookright', 'a bot eyes lookin', [18, 19, 20, 21, 22, 23, 24, 25, 26, 27, 28, 29, 30, 31, 32, 33, 34, 35], 24, false);
		eyes.anim.play('lookright', true);
		eyes.anim.curFrame = eyes.anim.length - 1;
		add(eyes);

		speaker = new FlxAnimate(-65, -10);
		Paths.loadAnimateAtlas(speaker, 'abot/abotSystem');
		speaker.anim.addBySymbol('anim', 'Abot System', 24, false);
		speaker.anim.play('anim', true);
		speaker.anim.curFrame = speaker.anim.length - 1;
		speaker.antialiasing = antialias;
		add(speaker);
		#end
	}

	#if funkin.vis
	var levels:Array<Bar>;
	var levelMax:Int = 0;
	/**
	 * Executes the `update` operation.
	 * @param elapsed Input value for `elapsed`.
	 */
	override function update(elapsed:Float):Void
	{
		super.update(elapsed);
		if(analyzer == null) return;

		levels = analyzer.getLevels(levels);
		var oldLevelMax = levelMax;
		levelMax = 0;
		for (i in 0...Std.int(Math.min(vizSprites.length, levels.length)))
		{
			var animFrame:Int = Math.round(levels[i].value * 5);
			animFrame = Std.int(Math.abs(FlxMath.bound(animFrame, 0, 5) - 5)); // shitty dumbass flip, cuz dave got da shit backwards lol!
		
			vizSprites[i].animation.curAnim.curFrame = animFrame;
			levelMax = Std.int(Math.max(levelMax, 5 - animFrame));
		}

		if(levelMax >= 4)
		{
			//trace(levelMax);
			if(oldLevelMax <= levelMax && (levelMax >= 5 || #if flxanimate speaker.anim.curFrame >= 3 #else false #end))
				beatHit();
		}
	}
	#end

	/**
	 * Executes the `beatHit` operation.
	 * @return Result produced by `beatHit`, when applicable.
	 */
	public function beatHit()
	{
		#if flxanimate
		speaker.anim.play('anim', true);
		#end
	}

	#if funkin.vis
	/**
	 * Executes the `initAnalyzer` operation.
	 * @return Result produced by `initAnalyzer`, when applicable.
	 */
	public function initAnalyzer()
	{
		#if !flash
		@:privateAccess
		#if (openfl > "9.2.2")
		analyzer = new SpectralAnalyzer(snd._channel.__audioSource, 7, 0.1, 40);
		#else
		analyzer = new SpectralAnalyzer(snd._channel.__source, 7, 0.1, 40);
		#end
	
		#if desktop
		// On desktop it uses FFT stuff that isn't as optimized as the direct browser stuff we use on HTML5
		// So we want to manually change it!
		analyzer.fftN = 256;
		#end
		#end
	}
	#end

	var lookingAtRight:Bool = true;
	/**
	 * Executes the `lookLeft` operation.
	 * @return Result produced by `lookLeft`, when applicable.
	 */
	public function lookLeft()
	{
		#if flxanimate
		if(lookingAtRight) eyes.anim.play('lookleft', true);
		#end
		lookingAtRight = false;
	}
	/**
	 * Executes the `lookRight` operation.
	 * @return Result produced by `lookRight`, when applicable.
	 */
	public function lookRight()
	{
		#if flxanimate
		if(!lookingAtRight) eyes.anim.play('lookright', true);
		#end
		lookingAtRight = true;
	}
}